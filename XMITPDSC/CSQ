/*
/* An edit macro to switch between the different forms of
/* square brackets used in C code.
/*
/* Invocation - Type CSQ on command line in edit of C source code
/*
/* The 3 types of square brackets are as follows:-
/*
/* x'AD' and x'BD' - Accepted by default in C compiler
/* ??( and ??)     - Accepted by default in C compiler
/*                   (known as trigraphs)
/* x'BA' and x'BB' - Some PC emulators map these characters to the
/*                   square bracket keys but they are not recognised
/*                   by the C compiler
/*
/* IBM sample CCNGMV2 improved by Paul Wells
/*

PROC 0
ISREDIT MACRO

SET RP = &STR())
 /* Symbolic values for 6 C language symbols.
 /* 1. left  bracket, ebcdic hex value
 /* 2. right bracket, ebcdic hex value
 /* 3. left  bracket, trigraph
 /* 4. right bracket, trigraph
 /* 5. left  bracket, square
 /* 6. right bracket, square
SET LBRACKET_HEX = X'AD'
SET RBRACKET_HEX = X'BD'
SET LBRACKET_TRI = &STR(??(
SET RBRACKET_TRI = &STR(??&RP)
SET LBRACKET_SQR = X'BA'       /* LBRACKET_SQR = HEX BA */
SET RBRACKET_SQR = X'BB'       /* RBRACKET_SQR = HEX BB */

ISREDIT FIND &LBRACKET_SQR ALL NX
ISREDIT (N1) = FIND_COUNTS
ISREDIT FIND &RBRACKET_SQR ALL NX
ISREDIT (N2) = FIND_COUNTS
/*IF (&N1 ¬= &N2) THEN WRITE ........UNBALANCED SQUARE BRACKETS*/
IF (&N1 > 0) THEN DO
   ISREDIT CHANGE &LBRACKET_SQR &LBRACKET_HEX ALL NX
   ISREDIT CHANGE &RBRACKET_SQR &RBRACKET_HEX ALL NX
   SET ZMSG000S = &STR(xAD/BD set)
   SET ZMSG000L = &STR(xAD/BD set)
   ISPEXEC VPUT (ZMSG000S,ZMSG000L)
   ISPEXEC SETMSG MSG(ISPZ000)
   EXIT
END

ISREDIT FIND &LBRACKET_HEX ALL NX
ISREDIT (N1) = FIND_COUNTS
ISREDIT FIND &RBRACKET_HEX ALL NX
ISREDIT (N2) = FIND_COUNTS
/*IF (&N1 ¬= &N2) THEN WRITE ........UNBALANCED HEX BRACKETS*/
IF (&N1 > 0) THEN DO
   ISREDIT CHANGE &LBRACKET_HEX &LBRACKET_TRI ALL NX
   ISREDIT CHANGE &RBRACKET_HEX &RBRACKET_TRI ALL NX
   SET ZMSG000S = &STR(Trigraphs ?? set)
   SET ZMSG000L = &STR(Trigraphs ?? set)
   ISPEXEC VPUT (ZMSG000S,ZMSG000L)
   ISPEXEC SETMSG MSG(ISPZ000)
   EXIT
END

ISREDIT FIND &LBRACKET_TRI ALL NX
ISREDIT (N1) = FIND_COUNTS
ISREDIT FIND &RBRACKET_TRI ALL NX
ISREDIT (N2) = FIND_COUNTS
/*IF (&N1 ¬= &N2) THEN WRITE ........UNBALANCED TRIGRAPH*/
IF (&N1 > 0) THEN DO
   ISREDIT CHANGE &LBRACKET_TRI &LBRACKET_SQR ALL NX
   ISREDIT CHANGE &RBRACKET_TRI &RBRACKET_SQR ALL NX
   SET ZMSG000S = &STR(xBA/BB set)
   SET ZMSG000L = &STR(xBA/BB set)
   ISPEXEC VPUT (ZMSG000S,ZMSG000L)
   ISPEXEC SETMSG MSG(ISPZ000)
   EXIT
END

